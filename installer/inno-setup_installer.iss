; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "PyTkinterToPSScript"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "MyPublisher"
#define MyAppURL "https://www.example.com"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{3564F68E-ACC3-4E0F-B743-28102309D015}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\PyTkinterToPSScript
DisableDirPage=yes
DefaultGroupName={#MyAppName}
; Uncomment the following line to run in non administrative install mode (install for current user only).
;PrivilegesRequired=lowest
OutputDir=C:\Users\Administrator\Documents\Git\python\PyTkinterToPSScript-UserDefined\installer
OutputBaseFilename=PyTkinterToPSScript-1.0.0
SolidCompression=yes
WizardStyle=modern
AlwaysRestart=yes

[Code]
function InitializeSetup(): Boolean;
var
  PSPath, PSArgs: String;
  ResultCode: Integer;
  Ans: Integer;
begin
  // インストール前に確認のメッセージボックスを表示する
  Ans := MsgBox('PowerShell(7.5.1)のインストールを開始します。続行しますか？', mbConfirmation, MB_YESNO);
  if Ans <> idYes then
  begin
    // ユーザーがキャンセルした場合は、インストーラーを中断する
    Result := False;
    Exit;
  end;
  
  // PowerShell の実行ファイルパスを取得
  PSPath := ExpandConstant('{sys}\WindowsPowerShell\v1.0\powershell.exe');
  
  // 実行する PowerShell コマンドレットを指定
  PSArgs := '-ExecutionPolicy Bypass -NoLogo -NonInteractive -WindowStyle Hidden -Command "winget install --id microsoft.powershell --version 7.5.1.0; exit $LASTEXITCODE"';
  
  // PowerShell を非表示で実行し、終了コードを取得する
  if Exec(PSPath, PSArgs, '', SW_HIDE, ewWaitUntilTerminated, ResultCode) then
  begin
    // 終了コードが 0(インストール完了) または -1978335189(インストール済み) の場合は成功
    if (ResultCode = 0) or (ResultCode = -1978335189) then
      Result := True
    else
    begin
      // PowerShell コマンドレットでエラーが発生（exit code が 0 以外）
      MsgBox('PowerShell コマンドレットの実行中にエラーが発生しました。' + 
             ' (エラーコード: ' + IntToStr(ResultCode) + ')', mbError, MB_OK);
      Result := False; // インストーラーを中断
    end;
  end
  else
  begin
    // Exec 関数自体の実行が失敗した場合
    MsgBox('PowerShell の実行に失敗しました。', mbError, MB_OK);
    Result := False; // インストーラーを中断
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  ResultCode: Integer;
begin
  // アンインストール完了直前（usPostUninstall）で実行
  if CurUninstallStep = usPostUninstall then
  begin
    if MsgBox('PowerShell のアンインストールを開始します。続行しますか？', mbConfirmation, MB_YESNO) = idYes then
    begin
      if not Exec(
          ExpandConstant('{sys}\WindowsPowerShell\v1.0\powershell.exe'),
          '-ExecutionPolicy Bypass -NoLogo -NonInteractive -WindowStyle Hidden -Command "winget uninstall --id microsoft.powershell; exit $LASTEXITCODE"',
          '', SW_HIDE, ewWaitUntilTerminated, ResultCode) then
      begin
        MsgBox('PowerShell のアンインストール実行に失敗しました。', mbError, MB_OK);
      end;
    end;
  end;
end;

[Languages]
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"

[Dirs]
Name: "{app}\input"
Name: "{app}\output"

[Files]
Source: "C:\Users\Administrator\Documents\Git\python\PyTkinterToPSScript-UserDefined\classes\config\*"; DestDir: "{app}\config"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\Administrator\Documents\Git\python\PyTkinterToPSScript-UserDefined\classes\exe\*"; DestDir: "{app}\exe"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\Administrator\Documents\Git\python\PyTkinterToPSScript-UserDefined\classes\script\*"; DestDir: "{app}\script"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\Administrator\Documents\Git\python\PyTkinterToPSScript-UserDefined\classes\template\*"; DestDir: "{app}\template"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\Administrator\Documents\Git\python\PyTkinterToPSScript-UserDefined\installer\PyTkinterToPSScript.exe - ショートカット.lnk"; DestDir: "{commondesktop}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
